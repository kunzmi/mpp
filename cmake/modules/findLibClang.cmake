#source of file: github.com/Pseudomanifold/libclang-experiments/

INCLUDE( FindPackageHandleStandardArgs )

IF( "${LLVM_DIR}" STREQUAL "" )
  SET( LLVM_DIR "" CACHE STRING "An optional hint to a libclang/llvm directory" )
ENDIF()

FIND_PATH( LIBCLANG_INCLUDE_DIR
  clang-c/Index.h
  PATH_SUFFIXES include
  HINTS ${LLVM_DIR}
)

FIND_LIBRARY( LIBCLANG_LIBRARY
  clang libclang
  PATH_SUFFIXES lib
  HINTS ${LLVM_DIR}
)

if (WIN32)
	FIND_FILE( LIBCLANG_BINARY
	  libclang.dll
	  PATH_SUFFIXES bin
	  HINTS ${LLVM_DIR}
	)
else()
	SET( LIBCLANG_BINARY ${LIBCLANG_LIBRARY})
endif()

FIND_PACKAGE_HANDLE_STANDARD_ARGS(
  LIBCLANG
  DEFAULT_MSG
  LIBCLANG_LIBRARY
  LIBCLANG_INCLUDE_DIR
  LIBCLANG_BINARY
)     

IF( LIBCLANG_FOUND )
  SET( LIBCLANG_LIBRARIES    ${LIBCLANG_LIBRARY} )
  SET( LIBCLANG_INCLUDE_DIRS ${LIBCLANG_INCLUDE_DIR} )
  SET( LIBCLANG_BINARIES     ${LIBCLANG_BINARY} )

  MARK_AS_ADVANCED(
    LIBCLANG_LIBRARY
    LIBCLANG_INCLUDE_DIR
    LIBCLANG_BINARY
  )

  add_library(LIBCLANG::LIBCLANG SHARED IMPORTED GLOBAL)
  set_target_properties(LIBCLANG::LIBCLANG PROPERTIES
		IMPORTED_LOCATION ${LIBCLANG_BINARIES}		
		IMPORTED_IMPLIB ${LIBCLANG_LIBRARY}
		INTERFACE_INCLUDE_DIRECTORIES ${LIBCLANG_INCLUDE_DIRS}
		)
ENDIF()